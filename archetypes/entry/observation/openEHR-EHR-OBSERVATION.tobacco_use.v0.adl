archetype (adl_version=1.4; uid=72014ae3-815a-4952-95c7-0b9b5fb744c2)
	openEHR-EHR-OBSERVATION.tobacco_use.v0

concept
	[at0000]	-- Tobacco Use
language
	original_language = <[ISO_639-1::en]>
	translations = <
		["nb"] = <
			language = <[ISO_639-1::nb]>
			author = <
				["name"] = <"Anca Heyd">
				["organisation"] = <"Dips ASA">
				["ahe@dips.no"] = <"ahe@dips.no">
			>
		>
	>
description
	original_author = <
		["name"] = <"Heather Leslie">
		["organisation"] = <"Ocean Informatics">
		["email"] = <"heather.leslie@oceaninformatics.com">
		["date"] = <"6/5/2009">
	>
	details = <
		["en"] = <
			language = <[ISO_639-1::en]>
			purpose = <"To record details about the actual use or consumption of tobacco and tobacco-containing products.">
			use = <"Use to record details of the use of all common forms of tobacco and tobacco-containing products. This includes actual use at specific point in time and average use over intervals of time.

A smoking or tobacco use diary could be built up over time by recording the use of tobacco on multiple, sequential days - recording actual consumptions using the 'Specified Day' event for each daily entry.

A record of typical tobacco use during a period can be recorded by recording the amount of tobacco used using the 'Average use' event - the mean use over a specified period of time.">
			keywords = <"tobacco", "cigarette", "cigar", "pipe", "snuff", "kretek", "beebi", "bibi", "shisha", "snuff", "snus", "hookah", "gutka", "smoking", "chewing">
			misuse = <"Not to be used for recording persistent, summary details about tobacco use and consumption - this should be captured using the EVALUATION.tobacco_use_summary archetype.">
			copyright = <"© Nasjonal IKT HF">
		>
		["nb"] = <
			language = <[ISO_639-1::nb]>
			purpose = <"Dokumentajon av aktuell bruk av tobakk og produkter som inneholder tobakk.">
			use = <"Brukes for å registrere bruk av alle vanlige former for tobakksbruk og bruk av produkter ssm inneholder tobakk. Dette inkluderer aktuell bruk til et gitt tidspunkt og gjennomsnittsbruk i en gitt periode.

En kan bygge opp en dagbok for røyking eller tobakksbruk ved å registrere tobaksbruk i en serieregistrering - bruk \"spesifisert dag\" event for hver daglig registrering.">
			keywords = <"Tobakk", "Sigaretter", "Sigar", "Pipe", "Snus", "kretek", "beebi", "bibi", "shisha", "hookah", "gutka", "røyking", "tyggetobakk">
			misuse = <"Skal ikke brukes for å registrere persistent, oppsummeringsdetaljer vedrørende tobakksbruk og forbruk - dette skal registreres ved å bruke EVALUATION.tobacco_use_summary arketypen.">
		>
	>
	lifecycle_state = <"in_development">
	other_contributors = <"Ian McNicoll, Ocean Informatics, UK", "Sam Heard, Ocean Informatics, Australia">
	other_details = <
		["revision"] = <"0.0.1-alpha">
		["licence"] = <"This work is licensed under the Creative Commons Attribution-ShareAlike 4.0 International License. To view a copy of this license, visit http://creativecommons.org/licenses/by-sa/4.0/.">
		["references"] = <"Tobacco Use draft archetype [Internet]. London: openEHR Clinical Knowledge Manager, openEHR Foundation; 06 May 2009 [cited 2011 Jul 15]. Available from: http://www.openehr.org/knowledge/OKM.html#showArchetype_1013.1.149_3.">
		["build_uid"] = <"3a713a0f-3707-4b20-a62b-0aece8d0094b">
		["original_publisher"] = <"Nasjonal IKT">
		["original_namespace"] = <"no.nasjonalikt">
		["MD5-CAM-1.0.1"] = <"3FFAE82526ED7FACF1FA7FFAE100EB79">
		["custodian_namespace"] = <"no.nasjonalikt">
		["custodian_organisation"] = <"Nasjonal IKT">
	>

definition
	OBSERVATION[at0000] matches {	-- Tobacco Use
		data matches {
			HISTORY[at0001] matches {	-- Event Series
				events cardinality matches {1..*; unordered} matches {
					EVENT[at0002] occurrences matches {0..1} matches {	-- Any event
						data matches {
							ITEM_TREE[at0003] matches {	-- Tree
								items cardinality matches {0..*; unordered} matches {
									ELEMENT[at0004] occurrences matches {0..1} matches {	-- Tobacco Use?
										value matches {
											DV_BOOLEAN matches {
												value matches {True, False}
											}
										}
									}
									CLUSTER[at0005] occurrences matches {0..*} matches {	-- Usage Details
										items cardinality matches {1..*; unordered} matches {
											ELEMENT[at0006] occurrences matches {0..1} matches {	-- Form
												value matches {
													DV_CODED_TEXT matches {
														defining_code matches {
															[local::
															at0007, 	-- Cigarette
															at0008, 	-- Roll-Your-Own
															at0009, 	-- Cigar
															at0010, 	-- Pipe
															at0012, 	-- Waterpipe
															at0013]	-- Smokeless tobacco
														}
													}
												}
											}
											CLUSTER[at0015] occurrences matches {0..1} matches {	-- Amount
												items cardinality matches {1; unordered} matches {
													ELEMENT[at0014] occurrences matches {0..1} matches {	-- Number Smoked
														value matches {
															C_DV_QUANTITY <
																property = <[openehr::382]>
																list = <
																	["1"] = <
																		units = <"/h">
																	>
																	["2"] = <
																		units = <"/d">
																	>
																	["3"] = <
																		units = <"/wk">
																	>
																	["4"] = <
																		units = <"/mo">
																	>
																>
															>
														}
													}
													ELEMENT[at0016] occurrences matches {0..1} matches {	-- Weight Consumed
														value matches {
															C_DV_QUANTITY <
																property = <[openehr::347]>
																list = <
																	["1"] = <
																		units = <"gm/d">
																	>
																	["2"] = <
																		units = <"gm/wk">
																	>
																>
															>
														}
													}
												}
											}
											ELEMENT[at0017] occurrences matches {0..1} matches {	-- Triggers
												value matches {
													DV_TEXT matches {*}
												}
											}
											ELEMENT[at0018] occurrences matches {0..1} matches {	-- Context
												value matches {
													DV_TEXT matches {*}
												}
											}
										}
									}
									ELEMENT[at0019] occurrences matches {0..1} matches {	-- Evidence of Dependence
										value matches {
											DV_TEXT matches {*}
										}
									}
									ELEMENT[at0021] occurrences matches {0..1} matches {	-- Comment
										value matches {
											DV_TEXT matches {*}
										}
									}
								}
							}
						}
					}
					POINT_EVENT[at0023] occurrences matches {0..*} matches {	-- Specified Day
						data matches {
							use_node ITEM_TREE /data[at0001]/events[at0002]/data[at0003]	-- /data[Event Series]/events[Any event]/data[Tree]
						}
					}
					INTERVAL_EVENT[at0022] occurrences matches {0..*} matches {	-- Average Use Interval
						math_function matches {
							DV_CODED_TEXT matches {
								defining_code matches {[openehr::146]}
							}
						}
						data matches {
							use_node ITEM_TREE /data[at0001]/events[at0002]/data[at0003]	-- /data[Event Series]/events[Any event]/data[Tree]
						}
					}
				}
			}
		}
	}


ontology
	term_definitions = <
		["en"] = <
			items = <
				["at0000"] = <
					text = <"Tobacco Use">
					description = <"Details about use of all forms of tobacco and tobacco-containing products at a specified point in time or over a specified period of time.">
				>
				["at0001"] = <
					text = <"Event Series">
					description = <"@ internal @">
				>
				["at0002"] = <
					text = <"Any event">
					description = <"An unspecified event.">
				>
				["at0003"] = <
					text = <"Tree">
					description = <"@ internal @">
				>
				["at0004"] = <
					text = <"Tobacco Use?">
					description = <"Is any tobacco being used or consumed?">
					comment = <"Record as True if the individual is using tobacco; Record as False if not using tobacco.">
				>
				["at0005"] = <
					text = <"Usage Details">
					description = <"Details about the use or consumption of specific forms of tobacco.">
				>
				["at0006"] = <
					text = <"Form">
					description = <"Form of tobacco used or consumed.">
				>
				["at0007"] = <
					text = <"Cigarette">
					description = <"Manufactured cigarettes, containing processed tobacco and rolled into a cylinder.">
				>
				["at0008"] = <
					text = <"Roll-Your-Own">
					description = <"Hand-filled cigarettes using loose tobacco rolled in a cigarette paper.">
				>
				["at0009"] = <
					text = <"Cigar">
					description = <"Air-cured and fermented tobaccos with a tobacco-leaf wrapper.">
				>
				["at0010"] = <
					text = <"Pipe">
					description = <"Tobacco placed in a bowl and smoke inhaled through a stem.">
				>
				["at0012"] = <
					text = <"Waterpipe">
					description = <"Tobacco, often flavoured, is burned then cooled through a basin of water eand consumed through a hose and mouthpiece.">
				>
				["at0013"] = <
					text = <"Smokeless tobacco">
					description = <"Tobacco consumed by inhalation or ingestion - including snuff, snus, gutka, dipping tobacco and dissolvable tobacco.">
				>
				["at0014"] = <
					text = <"Number Smoked">
					description = <"Number of units containing tobacco consumed.">
					comment = <"For example, eg cigarettes or cigars.">
				>
				["at0015"] = <
					text = <"Amount">
					description = <"Amount of tobacco used or consumed.">
					comment = <"Only one option allowed - either number smoked or weight consumed - per tobacco form.">
				>
				["at0016"] = <
					text = <"Weight Consumed">
					description = <"Weight of tobacco consumed.">
					comment = <"For example, smoked in a pipe or amount of chewing tobacco.">
				>
				["at0017"] = <
					text = <"Triggers">
					description = <"Identified triggers which stimulate or cause use of this form of tobacco.">
				>
				["at0018"] = <
					text = <"Context">
					description = <"Context of use of this form of tobacco.">
					comment = <"For example, when in a social situation, driving a car or talking on the telephone.">
				>
				["at0019"] = <
					text = <"Evidence of Dependence">
					description = <"Description of any evidence of dependence on tobacco.">
				>
				["at0021"] = <
					text = <"Comment">
					description = <"Additional narrative about the tobacco use or consumption not captured in other fields.">
				>
				["at0022"] = <
					text = <"Average Use Interval">
					description = <"Average, or typical, typical use over a specified time interval. For example, allows recording of average number of cigarettes smoked per week for the previous 10 years.">
				>
				["at0023"] = <
					text = <"Specified Day">
					description = <"Actual tobacco consumption on a specified day. Supports recording consumption in a Smoking Diary.">
				>
			>
		>
		["nb"] = <
			items = <
				["at0000"] = <
					text = <"Tobakksbruk">
					description = <"Detaljer vedrørende alle former av tobakk og produkter som inneholder tobakk på et gitt tidspunkt eller i en spesifisert periode.">
				>
				["at0001"] = <
					text = <"Event Series">
					description = <"@ internal @">
				>
				["at0002"] = <
					text = <"Uspesifisert hendelse">
					description = <"En uspesifisert hendelse">
				>
				["at0003"] = <
					text = <"Tree">
					description = <"@ internal @">
				>
				["at0004"] = <
					text = <"Tobakksbruk?">
					description = <"Brukes tobakk i en aller annen form?">
					comment = <"Registrer som Sann dersom individet bruker tobakk. Registrer som Usann dersom individet ikke bruker tobakk.">
				>
				["at0005"] = <
					text = <"Detaljer vedrørende bruk">
					description = <"Detaljer vedrørende bruk eller forbruk av spesifikke former tobakk.">
				>
				["at0006"] = <
					text = <"Form">
					description = <"Brukt tobakksform.">
				>
				["at0007"] = <
					text = <"Sigaretter">
					description = <"Ferdigproduserte sigaretter som inneholder bearbeidet tobakk som er rullet inn i en sylinder.">
				>
				["at0008"] = <
					text = <"Rulletobakk">
					description = <"Manuell rullede sigaretter hvor man har brukt løs tobakk og sigarett papir.">
				>
				["at0009"] = <
					text = <"Sigar">
					description = <"Luftbehandlet og fermentert tobakk rullet inn i et blad.">
				>
				["at0010"] = <
					text = <"Pipe">
					description = <"Tobakk plassert i en beholder og røyken inhalert via en stilk.">
				>
				["at0012"] = <
					text = <"Vannpipe">
					description = <"Tobakk som ofte er aromatisert brennes og kjøles ned i en vannbeholder og inhlaeeres gjennom en slange og et munnstykke.">
				>
				["at0013"] = <
					text = <"Tobakk uten røyk">
					description = <"tobakk som brukes ved å svelge eller inhalere - inkluderer snus, gutka, oppløsbar og ... tobakk.">
				>
				["at0014"] = <
					text = <"Antall røyket">
					description = <"Antall enheter som inneholder tobakk som er røyket.">
					comment = <"For ekspempel: sigaretter eller sigarrer.">
				>
				["at0015"] = <
					text = <"Mengde">
					description = <"Tobakksmengde som brukes.">
					comment = <"Bare en opsjon er tillatt per tobakksform - enten antall røykt eller vekt konsumert.">
				>
				["at0016"] = <
					text = <"Brukt vekt">
					description = <"Vekten til tobakksmengden som er konsumert.">
					comment = <"For ekspempel, røket i en pipe eller mengde tyggetobakk.">
				>
				["at0017"] = <
					text = <"Utløser">
					description = <"Identifiserte utløsere som stimulerer er forårsaker bruk av angitt tobakksform.">
				>
				["at0018"] = <
					text = <"Kontekst">
					description = <"Konteksten som man er i når man bruker angitt tobakksform.">
					comment = <"For eksempel, i en sosial situasjon, ved bilkjøring eller når man snakker i telefon.">
				>
				["at0019"] = <
					text = <"Evidens for avhengighet">
					description = <"Beskrivelse av enhver form for evidens for avhengieht av tobakk.">
				>
				["at0021"] = <
					text = <"Kommentar">
					description = <"Supplerende tekst vedrørende tobakksbruk eller vorburk som ikke er fanget opp i andre felter.">
				>
				["at0022"] = <
					text = <"Gjennomsnittlig bruksintervall">
					description = <"Gjennomsnittlig eller typisk bruk i et gitt tidsintervall. tillater for eksempel å registrere gjennomsnitts antall sigaretter som røykes per uke de siste 10 årene.">
				>
				["at0023"] = <
					text = <"Spesifisert dag">
					description = <"Aktuell tobakksbruk på en spesifisert dag. Støtter under registrering i en tobakksdagbok.">
				>
			>
		>
	>
